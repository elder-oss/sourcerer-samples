buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.sonarqube" version "2.6.2"
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

System.setProperty('sonar.projectVersion', getVersionName())

subprojects {
    group = 'org.elder'
    version = getVersionName()

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    apply plugin: "jacoco"

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()

            force "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            force "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
            force "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
            force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
            force "org.slf4j:slf4j-api:1.7.21"
            force "org.jetbrains:annotations:15.0"
            force "io.swagger:swagger-annotations:1.5.8"
            force "com.google.guava:guava:24.1-jre"
            force "com.fasterxml.jackson.core:jackson-annotations:2.9.0"
            force "com.fasterxml.jackson.core:jackson-databind:2.9.7"
            force "org.springframework:spring-core:4.3.6.RELEASE"
            force "org.springframework:spring-beans:4.3.6.RELEASE"
            force "org.springframework:spring-expression:4.3.6.RELEASE"
            force "org.springframework:spring-context:4.3.6.RELEASE"
            force "org.springframework:spring-aop:4.3.6.RELEASE"
            force "org.hamcrest:hamcrest-core:1.3"
            force "junit:junit:4.12"
            force "org.slf4j:jcl-over-slf4j:1.7.21"
            force "org.jboss.logging:jboss-logging:3.3.0.Final"
            force "com.fasterxml:classmate:1.3.1"
        }
    }

    checkstyle {
        configFile = new File(rootDir, "infra/codehealth/checkstyle.xml")
        toolVersion = "7.1.1"
        ignoreFailures = false
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jacoco {
        reportsDir = file("build/coverage/jacoco.exec")
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile 'ch.qos.logback:logback-classic:1.1.7'
        compile 'org.slf4j:jcl-over-slf4j:1.7.21'
        testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'org.mockito:mockito-core:1.10.19'
        testCompile 'org.powermock:powermock-mockito-release-full:1.6.4'
    }

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    task downloadDependencies(type: Copy) {
        from configurations.testRuntime
        into 'build/tmp/dependencieshack/'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }
}
